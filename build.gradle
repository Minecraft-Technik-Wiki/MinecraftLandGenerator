plugins {
	id 'java-library'
	id 'maven'
	id 'application'
	id "com.github.johnrengelman.shadow" version "5.0.0"
	id 'eclipse'
	id 'idea'
}

mainClassName = "de.piegames.mlg.MinecraftLandGenerator"
group = 'de.piegames'
version = '3.1.1'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

dependencies {
	api 'org.slf4j:slf4j-api:1.7.26'
	implementation 'org.apache.logging.log4j:log4j-core:2.11.1'
	implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.1'

	api 'org.joml:joml:1.9.14'
	api 'info.picocli:picocli:3.7.0'
	implementation 'com.google.code.gson:gson:2.8.5'
	implementation 'com.github.piegamesde:nbt:fd6d00577b1ae1f2c90dc1328fcf2b46c700e67d'
	implementation 'io.github.soc:directories:11'
	testCompile 'junit:junit:4.12'
}

task release(type: GradleBuild) {
	group 'Distribution'
	tasks = ['clean', 'createPom', 'jar', 'shadowJar']
}

task createPom {
    pom {
        project {
            groupId 'de.piegames'
            artifactId 'mlg'
            version "${version}"

            licenses {
                license {
                    name 'CC0 Universal'
                    url 'http://www.creativecommons.org/publicdomain/zero/1.0/'
                    distribution 'repo'
                }
            }
        }
    }.writeTo("pom.xml")
}

jar {
	exclude("log4j2.xml")
	manifest {
		attributes 'Implementation-Title': 'MinecraftLandGenerator',
		'Main-Class': 'de.piegames.mlg.MinecraftLandGenerator'
	}
	appendix 'lib'
}

shadowJar {
	classifier = ''
}

// Hide task
knows.group = null
